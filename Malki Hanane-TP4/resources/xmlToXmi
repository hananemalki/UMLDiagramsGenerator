<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
                xmlns:xmi="http://www.omg.org/spec/XMI/20131001" 
                xmlns:uml="http://www.eclipse.org/uml2/5.0.0/UML"
                version="2.0">
    
    <xsl:template match="/project">
        <xmi:XMI xmi:version="2.5">
            <uml:Model xmi:type="uml:Model" xmi:id="rootModel" name="{@name}">
                <xsl:apply-templates select="package"/>
            </uml:Model>
        </xmi:XMI>
    </xsl:template>

    <xsl:template match="package">
        <packagedElement xmi:type="uml:Package" xmi:id="{generate-id()}" name="{@name}">
            <xsl:apply-templates select="class"/>
            <xsl:apply-templates select="interface"/>
            <xsl:apply-templates select="enum"/>
        </packagedElement>
    </xsl:template>

    <xsl:template match="class">
        <packagedElement xmi:type="uml:Class" xmi:id="{generate-id()}" name="{name}" visibility="{@modifier}">
            <xsl:apply-templates select="fields/field"/>
            <xsl:apply-templates select="methods/method"/>
            <xsl:apply-templates select="relations/relation"/>
        </packagedElement>
    </xsl:template>

    <xsl:template match="field">
        <ownedAttribute xmi:type="uml:Property" xmi:id="{generate-id()}" name="{name}" visibility="{modifier}">
            <type xmi:type="uml:PrimitiveType" href="pathmap://UML_LIBRARIES/UMLPrimitiveTypes.library.uml#{type}"/>
        </ownedAttribute>
    </xsl:template>

    <xsl:template match="method">
        <ownedOperation xmi:type="uml:Operation" xmi:id="{generate-id()}" name="{name}" visibility="{modifier}">
            <ownedParameter xmi:type="uml:Parameter" xmi:id="{concat(generate-id(), '_return')}" direction="return">
                <type xmi:type="uml:PrimitiveType" href="pathmap://UML_LIBRARIES/UMLPrimitiveTypes.library.uml#{returnType}"/>
            </ownedParameter>
            <xsl:apply-templates select="parameters/parameter"/>
        </ownedOperation>
    </xsl:template>

    <xsl:template match="parameter">
        <ownedParameter xmi:type="uml:Parameter" xmi:id="{generate-id()}" name="{name}">
            <type xmi:type="uml:PrimitiveType" href="pathmap://UML_LIBRARIES/UMLPrimitiveTypes.library.uml#{type}"/>
        </ownedParameter>
    </xsl:template>

    <xsl:template match="relation">
        <xsl:choose>
            <xsl:when test="type = 'extends'">
                <generalization xmi:type="uml:Generalization" xmi:id="{generate-id()}" general="{target}"/>
            </xsl:when>
            <xsl:when test="type = 'aggregation'">
                <ownedAttribute xmi:type="uml:Property" xmi:id="{generate-id()}" name="{target}" aggregation="shared">
                    <type xmi:type="uml:Class" href="#{target}"/>
                </ownedAttribute>
            </xsl:when>
            <xsl:when test="type = 'composition'">
                <ownedAttribute xmi:type="uml:Property" xmi:id="{generate-id()}" name="{target}" aggregation="composite">
                    <type xmi:type="uml:Class" href="#{target}"/>
                </ownedAttribute>
            </xsl:when>
        </xsl:choose>
    </xsl:template>

    <xsl:template match="interface">
        <packagedElement xmi:type="uml:Interface" xmi:id="{generate-id()}" name="{name}">
            <xsl:apply-templates select="methods/method"/>
        </packagedElement>
    </xsl:template>

    <xsl:template match="enum">
        <packagedElement xmi:type="uml:Enumeration" xmi:id="{generate-id()}" name="{name}">
            <xsl:apply-templates select="values/value"/>
        </packagedElement>
    </xsl:template>

</xsl:stylesheet>